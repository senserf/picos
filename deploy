#!/bin/sh
###################\
exec tclsh "$0" "$@"
#
# Comprehensively and consistently installs the complete set: PICOS, VUEE, SIDE,
# PIP verifying version dependencies
#

###############################################################################
# Determine the system type ###################################################
###############################################################################

if [catch { exec uname } ST(SYS)] {
	set ST(SYS) "W"
} elseif [regexp -nocase "linux" $ST(SYS)] {
	set ST(SYS) "L"
} elseif [regexp -nocase "cygwin" $ST(SYS)] {
	set ST(SYS) "C"
} else {
	set ST(SYS) "W"
}

###############################################################################

set TCLVER [info tclversion]
set TCL85 [list "85" "8.5"]

if { 1 || $ST(SYS) == "L" } {
	if { [string first "-l" $argv] < 0 && $TCLVER >= 8.5 } {
		set TCL85 ""
	}
} else {
	if { [string first "-n" $argv] >= 0 && $TCLVER >= 8.5 } {
		set TCL85 ""
	}
}

if { $TCL85 == "" } {
	puts "==============================================="
	puts "Using native Tcl (version $TCLVER) for all scripts!"
	puts "==============================================="
}

###############################################################################

set PLIST { PICOS SIDE VUEE PIP }
set OPTIONAL ""

proc errab { m } {

	puts stderr $m
	exit 99
}

###############################################################################

proc pexists { pkg } {

	global CWD

	return [file isdirectory [file normalize [file join $CWD .. $pkg]]]
}

proc install { pkg } {

	global CWD HOME BIN PPATH OPTIONAL REL

	if ![pexists $pkg] {
		if { [lsearch -exact $OPTIONAL $pkg] < 0 } {
			errab "Package $pkg not found!"
		}
		puts "Skipping optional package $pkg"
		return
	}

	cd $CWD
	set tar "../$pkg"
	cd $tar
	puts "Deploying $pkg ..."
	if [catch { exec sh deploy $BIN $PPATH $REL($pkg) >@ stdout } err] {
		errab "Failed to deploy $pkg: $err"
	}

	cd $CWD
}

proc cmp_tags { a b } {
#
# Compares two tags, returns "", -1, 0, 1
#

	set pat "(\[0-9\]\[0-9\]\[0-9\]\[0-9\]\[0-9\]\[0-9\])(\[A-Z\])?$"

	set leta "a"
	if ![regexp $pat $a jnk numa leta] {
		# failed
		return ""
	}
	set numa "1$numa"

	set letb "a"
	if ![regexp $pat $b jnk numb letb] {
		return ""
	}
	set numb "1$numb"


	if { $numa < $numb } {
		return -1
	}

	if { $numa > $numb } {
		return 1
	}

	# the tricky case, have to look at the letters

	set leta [string tolower $leta]
	set letb [string tolower $letb]

	if { $leta == $letb } {
		return 0
	}

	if { [lindex [lsort [list $leta $letb]] 0] == $leta } {
		return -1
	}

	return 1
}

proc get_rtags { pkg } {
#
# Scans through RTAGS in the current directory extracting the most recent 
# release tag and dependencies
#
	global CWD PLIST PVER

	cd $CWD
	set dir [file normalize "../$pkg"]

	if [catch { cd $dir } ] {
		errab "Package $pkg is not present at $dir"
	}

	set fl [glob *]
	set rt "X000000A"
	set maj 0
	set min 0

	set dlist ""
	foreach d $PLIST {
		if { $d != $pkg } {
			lappend dlist [list $d $rt]
		}
	}

	set ptg "(\[0-9A-Z\]+):"
	set ptd ">=\[ \t\]*(\[0-9A-Z\]+)"
	set pve "\[0-9A-Z\]+:\[ \t\]*(\[0-9\]+)\[ \t\]+(\[0-9\]+)"


	foreach f $fl {

		if ![regexp "^RTAGS" $f] {
			continue
		}

		if [catch { open $f "r" } fd] {
			errab "Cannot open $f in $pkg: $fd"
		}

		if [catch { read $fd } rtf] {
			errab "Cannot read $f in $pkg: $rtf"
		}
		catch { close $fd }

		# the last RTAG
		if { ![regexp -nocase ".*\n$ptg" $rtf fd rtag] &&
		     ![regexp -nocase "^$ptg" $rtf fd rtag] } {
			continue
		}
		set cmp [cmp_tags $rt $rtag]
		if { $cmp == "" } {
			errab "Illegal tag '$rtag' in $f in $pkg"
		} elseif { $cmp < 0 } {
			set rt $rtag
		}

		# look for dependencies

		set nl ""
		foreach d $dlist {

			set pac [lindex $d 0]
			set tag [lindex $d 1]

			if { [regexp -nocase ".*\n$ptg\[^\n\]$pac\[ \t\]*$ptd" \
			       $rtf fd fd rtag] ||
			     [regexp -nocase "^$ptg\[^\n\]$pac\[ \t\]*$ptd" \
			       $rtf fd fd rtag] } {

				set cmp [cmp_tags $tag $rtag]
				if { $cmp == "" } {
					errab "Illegal dependency tag\
						'$rtag' in $f in $pkg"
				} elseif { $cmp < 0 } {
					set tag $rtag
				}
			}

			lappend nl [list $pac $tag]
		}
		set dlist $nl

		if { $pkg == "PICOS" } {
			# look for version
			if { ![regexp -nocase ".*\n$pve" $rtf fd ma mi] &&
		     	![regexp -nocase "^$pve" $rtf fd ma mi] } {
				continue
			}

			if { [catch { expr $ma } mar] ||
			     [catch { expr $mi } mir] } {
				errab "Illegal version numbers '$ma $mi' in $f"
			}

			if { $ma > $maj || ( $ma == $maj && $mi > $min ) } {
				set maj $ma
				set min $mi
			}
		}
	}

	if { $maj != 0 } {
		set PVER "$maj.$min"
	}

	# get back where you started from
	cd $CWD

	return [list $rt $dlist]
}

proc anchor { } {
#
# Locate the root directory of PICOS (i.e., from where we should be called)
#
	global CWD RWD BIN env HOME PPATH

	set CWD [pwd]

	while 1 {
		if { [file isdirectory "Apps"] && [file isdirectory "PicOS"] &&\
		     [file isdirectory "Scripts"] } {
			break
		}
		set nwd [file normalize "${CWD}/.."]
		if { $nwd == $CWD || [catch { cd $nwd }] } {
			errab "cannot locate root directory of the package"
		}
		puts "$CWD does not include a complete PICOS tree, trying $nwd"
		set CWD $nwd
	}

	# determine the location of HOME/bin

	if ![info exists env(HOME)] {
		errab "Cannot determine HOME directory"
	}

	set HOME [file normalize $env(HOME)]

	set BIN ""
	foreach bc { bin BIN } {
		if [file isdirectory [file join $HOME $bc]] {
			set BIN $bc
			break
		}
	}

	if { $BIN == "" } {
		puts "Warning: neither bin nor BIN found in HOME, will create\
			bin"
		set BIN "bin"
	}

	set PPATH [file normalize ".."]
	if { [string first $HOME $PPATH] != 0 } {
		if [regexp -nocase "^(\[a-z\]):(/.*)" $PPATH jnk dr ta] {
			# we are on Cygwin and this is a different drive
			set PPATH "/cygdrive/$dr$ta"
			if [file isdirectory $PPATH] {
				return
			}
		}
		errab "Cannot locate VUEE path"
	}

	set PPATH [string trim [string range $PPATH [string length $HOME] end] \
		"/"]
}

proc ver_dep { depl } {
#
# Verify version dependencies
#
	global PVER REL

	set out ""

	foreach u $depl {

		set p [lindex $u 0]
		set d [lindex $u 1]
		# package's own release tag
		set r [lindex $d 0]

		set REL($p) $r

		if { $out != "" } {
			append out ", "
		}
		if { $r == "X000000A" } {
			puts "Warning: $p has no release tag"
			set r "none"
		}
		append out "$p = $r"
		if { $p == "PICOS" && [info exists PVER] } {
			append out " ($PVER)"
		}
	}

	puts "Release tags: $out"

	set ok 1

	foreach u $depl {

		set p [lindex $u 0]
		set d [lindex $u 1]
		# dependencies
		set d [lindex $d 1]
		foreach m $d {
			set pac [lindex $m 0]
			set tag [lindex $m 1]
			if { $tag == "X000000A" } {
				continue
			}
			# puts "$p <- $pac release $REL($pac)"
			set cmp [cmp_tags $REL($pac) $tag]
			if { $cmp == "" } {
				errab "Cannot compare tags $tag (of $p) and\
					$REL($pac) of $pac"
			}
			if { $cmp < 0 } {
				puts "$p requires release $tag (or higher)\
					of $pac"
				set ok 0
			}
		}
	}

	if !$ok {
		exit 99
	}
}

proc copyscript { src nam suf tar rtg } {
#
# Copy script, make sure permissions are right, and optionally create a
# link
#
	set fis [file join $src $nam]
	set fit "[file rootname [file join $tar $nam]]$suf"
	set pgm [file tail $fit]

	catch { file delete -force -- $fit }
	catch { file delete -force -- [file rootname $fit] }

	if [catch { exec sed -e "s/ZZ000000A/$rtg/g" < $fis > $fit } err] {
		puts "Cannot copy $fis to $fit ($err), script ignored!"
		return
	}

	set cwd [pwd]

	if [catch { cd $tar } err] {
		puts "Cannot cd to $tar ($err)!"
	} else {
		catch { exec chmod 0755 $pgm }
		if { $suf != "" } {
			set lk [file rootname $pgm]
			catch { exec ln -sf $pgm $lk }
		}
	}

	cd $cwd
}

proc fixtcl85 { pgm } {

	global BIN BINW TCL85 ST

	set fn "$pgm.tcl"
	if [catch { open $fn "r" } fd] {
		set fn $pgm
		if [catch { open $fn "r" } fd] {
			puts "Cannot open $fn, ignored!"
			return
		}
	}

	if [catch { read $fd } txt] {
		puts "Cannot read $pgm from $BIN, ignored!"
		catch { close $fd }
		return
	}

	catch { close $fd }

	set trg "[file join $BIN $pgm]"

	if ![regexp "\nexec *(tclsh|wish)\[^\n\]*" $txt match what] {
		puts "Cannot locate tcl reference in $pgm, ignored!"
		return
	}

	set ix [string first $match $txt]

	set in "\nexec "
	if { [string index $what 0] == "t" } {
		append in "tclsh"
	} else {
		append in "wish"
	}

	if { $TCL85 != "" } {
		append in $TCL85
		# this is a Cygwin-unaware version, so make sure the path is
		# fully specified
		append in " $BINW/$fn"
	} else {
		append in " \"\$0\""
	}

	append in " \"\$@\""

	set txt "[string range $txt 0 [expr $ix - 1]]$in[string range $txt \
		[expr $ix + [string length $match]] end]"

	catch { exec rm -rf $fn }
	catch { exec cat > $fn << $txt }
	catch { exec chmod 0755 $trg }
}

proc doit { } {

	global CWD PLIST BIN BINW HOME TCL85 ST REL

	anchor

	# verify dependencies

	set depl ""

	foreach p $PLIST {
		if [pexists $p] {
			lappend depl [list $p [get_rtags $p]]
		}
	}

	ver_dep $depl

	foreach p $PLIST {

		if { $p != "PICOS" } {
			install $p
		}
	}

	puts "Deploying PICOS ..."

	set BIN [file join $HOME $BIN]

	if [catch { file mkdir $BIN } err] {
		errab "Cannot access $BIN: $err"
	}

	if { $ST(SYS) == "L" } {
		set BINW $BIN
	} else {
		# get the windows path to BIN
		set BINW [exec cygpath -m $BIN]
	}

	# copy scripts (for PICOS and VUEE); PIP did it itself
	copyscript "Scripts" "mkmk" "" $BIN $REL(PICOS)
	copyscript "Scripts" "picomp" "" $BIN $REL(PICOS)
	copyscript "Scripts" "usefetdll" "" $BIN $REL(PICOS)
	copyscript "Scripts" "piter.tcl" ".tcl" $BIN $REL(PICOS)
	copyscript "Scripts" "oss.tcl" "" $BIN $REL(PICOS)
	copyscript "../VUEE/UDAEMON" "udaemon" ".tcl" $BIN $REL(VUEE)

	set ScriptsToFix { pip piter udaemon genimage gdbloader oss }

	set tcldir ""
	if { $TCL85 != "" } {
		foreach tc $TCL85 {
			if ![catch { exec which "tclsh$tc" } tcp] {
				if { [string first "which:" $tcp] < 0 } {
					set tcldir $tcp
					set TCL85 $tc
					break
				}
			}
		}
		if { $tcldir == "" } {
		    puts "====================================================="
		    puts "You don't seem to have Tcl8.5 anywhere on your  PATH,"
		    puts "so  I cannot fix those scripts that require Tcl8.5 to"
		    puts "use it. This may cause problems.                     "
		    puts "====================================================="
		    set ScriptsToFix ""
		}
	}

	if [catch { cd $BIN } err] {
		puts "Cannot cd to $BIN, $err!"
		exit 99
	}

	foreach tc $ScriptsToFix {
		fixtcl85 $tc
	}

	catch {
		set fd [open "picospath" "w"]
		puts $fd "#!/bin/sh\necho $CWD"
		close $fd
	}

	catch { exec chmod 0755 picospath }
	puts "All done"
}

doit
